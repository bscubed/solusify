# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop-full
pkgver=10.5.1
pkgrel=1
pkgdesc="Budgie Desktop is the flagship desktop experience developed by the Solus team"
arch=('x86_64')
url="https://github.com/bscubed/budgie-desktop"
license=('GPL' 'LGPL')
provides=('budgie-desktop')
conflicts=('budgie-desktop')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=e6433d87974e735296e72cf5b9d2a25466d5fe7a  # tags/v10.5.1
source=("$pkgname"::"git+https://github.com/solus-project/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "desktop-override.patch"
        "animation-patch.patch")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            'baffb1d64715c20c1ae444a5b8ea0f8e9bf69cccf30e5e2c6729fe6d0c7491d2'
            '0fe35fd206674a715047f1676bca020f6f972bdec283a3dfa319fdaa9d00e413')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	cd $pkgname

	# Override settings only for Budgie
	patch -Np1 -i ../desktop-override.patch

	# Patch to enable 
	patch -Rp1 -i ../animation-patch.patch

	# Don't use systemd session management, it's not supported by Budgie
	sed -i 's/exec gnome-session/exec gnome-session --builtin/' src/session/budgie-desktop.in

	git submodule init
	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update
}

build() {
	arch-meson $pkgname build -D with-desktop-icons=none
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
